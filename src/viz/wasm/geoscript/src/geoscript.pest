WHITESPACE                   = _{ " " | "\t" | "\n" | "\r" }
COMMENT                      = _{ "//" ~ (!"\n" ~ ANY)* }

program                      = { SOI ~ statement* ~ EOI }
statement                    = _{ (return_statement | break_statement | assignment | expr) ~ ";"? ~ EOL* }
return_statement             = { "return" ~ expr? ~ ";"? }
break_statement              = { "break" ~ expr? ~ ";"? }
EOL                          = _{ WHITESPACE* ~ ("\n" | "\r\n") }

assignment                   = { ident ~ type_hint? ~ "=" ~ expr }
type_hint                    = { ":" ~ ident }

expr                         = { prefix_op* ~ term ~ postfix* ~ (operator ~ prefix_op* ~ term ~ postfix*)* }

// TODO: ternary would be cool at some point

prefix_op                    = _{ neg_op | pos_op | negate_op }
parenthized_term             = _{ "(" ~ expr ~ ")" }
term                         = { array_literal | closure | func_call | range_inclusive_literal_expr | range_literal_expr
                                 | block_expr | if_expression | literal | string_literal | ident | parenthized_term }
postfix                      = { field_access_op ~ ident }

// need these to work around ambiguity where a range expression starting with an
// integer literal looks like a float
range_literal_expr           = { int ~ ".." ~ term }
range_inclusive_literal_expr = { int ~ "..=" ~ term }

if_expression                = { "if" ~ expr ~ block_expr ~ else_if_expr* ~ else_expr? }
else_if_expr                 = { "else if" ~ expr ~ block_expr }
else_expr                    = { "else" ~ block_expr }
block_expr                   = { "{" ~ statement* ~ "}" }

operator                     = _{ map_op | range_inclusive_op | range_op | add_op | sub_op
                                  | mul_op | div_op | mod_op | gte_op | lte_op | gt_op | lt_op
                                  | neq_op | eq_op | or_op | and_op | bit_and_op | pipeline_op }

neg_op                       = { "-" }
pos_op                       = { "+" }
negate_op                    = { "!" }

range_op                     = { ".." }
range_inclusive_op           = { "..=" }
pipeline_op                  = { "|" }
map_op                       = { "->" }
add_op                       = { "+" }
sub_op                       = { "-" }
mul_op                       = { "*" }
div_op                       = { "/" }
mod_op                       = { "%" }
gte_op                       = { ">=" }
lte_op                       = { "<=" }
gt_op                        = { ">" }
lt_op                        = { "<" }
neq_op                       = { "!=" }
eq_op                        = { "==" }
and_op                       = { "&&" }
or_op                        = { "||" }
bit_and_op                   = { "&" }
field_access_op              = { "." }

func_call                    = { ident ~ "(" ~ arg_list? ~ ")" }
arg_list                     = _{ (arg ~ ("," ~ arg)*)? ~ ","? }
arg                          = { keyword_arg | expr }
keyword_arg                  = { ident ~ "=" ~ expr }

closure                      = { "|" ~ fn_def_arg_list ~ "|" ~ type_hint? ~ closure_body }
fn_def_arg                   = { ident ~ type_hint? ~ ("=" ~ fn_arg_default_val)? }
// limiting the things that can be used as an arg default value to prevent the syntax
// from spiraling out of control and avoiding horrific edge cases like `|x: int = 1 | 1| 1 || 1`
fn_arg_default_val           = { ident | literal | parenthized_term | func_call | block_expr | if_expression }
fn_def_arg_list              = { (fn_def_arg* ~ ("," ~ fn_def_arg)*)}
closure_body                 = _{ bracketed_closure_body | expr }
bracketed_closure_body       = { "{" ~ statement* ~ "}" }

array_literal                = { "[" ~ (expr ~ ("," ~ expr)*)? ~ ","? ~ "]" }

bool_literal                 = { "true" | "false" }
nil_literal                  = { "nil" }
literal                      = _{ float | int | bool_literal | nil_literal }
int                          = @{ ASCII_DIGIT+ }
float                        = @{ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
string_literal               = _{ single_quote_string_literal | double_quote_string_literal }
single_quote_string_literal  = @{ "'" ~ single_quote_string_inner ~ "'" }
escaped_single_quote         = @{ "\\'" }
unescaped_apos_str_content   = { (!("'" | "\\") ~ ANY)* }
single_quote_string_inner    = ${ unescaped_apos_str_content ~ (escaped_single_quote ~ single_quote_string_inner)? }
double_quote_string_literal  = @{ "\"" ~ double_quote_string_inner ~ "\"" }
escaped_double_quote         = @{ "\\\"" }
unescaped_quote_str_content  = { (!("\"" | "\\") ~ ANY)* }
double_quote_string_inner    = ${ unescaped_quote_str_content ~ (escaped_double_quote ~ double_quote_string_inner)? }

ident                        = @{ (ASCII_ALPHA | "_")+ ~ (ASCII_ALPHANUMERIC | "_")* }
