@precedence {
  call
  unary
  fieldAccess @left
  mul @left
  add @left
  bitAnd @left
  compare @left
  and @left
  or @left
  bitOr @left
  assign @right
  range @left
  return @left
  break @left
  cond
  statement
}

@skip { whitespace | LineComment }

@top Program {
  statement*
}

DestructureElem { Identifier | ArrayDestructure | MapDestructure }
MapDestructureKV { Identifier ":" DestructureElem }
MapDestructureElem { MapDestructureKV | Identifier }
ArrayDestructure { "[" (DestructureElem ~maybedestructure ("," DestructureElem?)*)? "]" }
MapDestructure { "{" (MapDestructureElem ~maybedestructure ("," MapDestructureElem?)*)? "}" }

statement {
  ReturnStatement { Return !return Expr? ";"? } |
  BreakStatement { Break !break Expr? ";"? } |
  ExprStatement { Expr ";"? } |
  DestructureAssignmentStatement { (ArrayDestructure | MapDestructure) "=" Expr ";"? } |
  AssignmentStatement { Identifier TypeHint? "=" Expr ";"? }
}

TypeHint { ":" Identifier }

UnaryOp { "+" | "-" | "!" }

ParenthesizedExpr { "(" Expr ")" }
CallExpr { Identifier !call "(" commaSep<FunctionArg> ")" }
IfExpr { If !cond Expr BlockExpr ElseIfExpr* ElseExpr? }

FnArgDefaultValue { Identifier | literal | ArrayExpr | MapExpr | ParenthesizedExpr | CallExpr | BlockExpr | IfExpr }

ClosureParamList {
  Pipe commaSep<Parameter { (Identifier | ArrayDestructure | MapDestructure) TypeHint? ("=" FnArgDefaultValue)? }> Pipe
}

ArrayExpr { "[" (Expr ~maybedestructure ("," Expr?)*)? "]" }
MapExpr { "{" (MapEntry ~maybedestructure ("," MapEntry)*)? ","? "}" ~maybeblock }

Expr {
  UnaryExpr { UnaryOp !unary Expr } |
  BinaryExpr |
  StaticFieldAccessExpr |
  FieldAccessExpr |
  RangeExpr { Expr !range (".." | "..=") Expr? } |
  CallExpr |
  literal |
  Identifier |
  ArrayExpr |
  MapExpr |
  ClosureExpr { (LogicalOrOp TypeHint? Expr) | (ClosureParamList TypeHint? Expr) } |
  ParenthesizedExpr |
  BlockExpr |
  IfExpr
}

BlockExpr { "{" statement* ~maybedestructure "}" ~maybeblock }
ElseIfExpr { ElseIf !cond Expr BlockExpr }
ElseExpr { Else BlockExpr }

FunctionArg {
  Expr | Kwarg { Identifier "=" Expr }
}

BinaryExpr {
  Expr !add ("+" | "-") Expr |
  Expr !mul ("*" | "/" | "%") Expr |
  Expr !bitAnd "&" Expr |
  Expr !bitOr Pipe Expr |
  Expr !bitOr "->" Expr |
  Expr !compare CompareOp Expr |
  Expr !and LogicalAndOp Expr |
  Expr !or LogicalOrOp Expr
}

StaticFieldAccessExpr {
  Expr !fieldAccess "." Identifier
}

FieldAccessExpr {
  Expr !fieldAccess "[" Expr "]"
}

literal { Float | HexInteger | Integer | BoolLiteral | NilLiteral | StringLiteral }

StringLiteral[isolate] { SingleQuoteStringLiteral | DoubleQuoteStringLiteral }

escapedBackslash { "\\\\" }
escapedDoubleQuote { "\\\"" }
escapedSingleQuote { "\\'" }

@skip {} {
  DoubleQuoteStringLiteral[isolate] { '"' (escapedBackslash | escapedDoubleQuote | doubleQuoteStringContent)* '"' }
  SingleQuoteStringLiteral[isolate] { "'" (escapedBackslash | escapedSingleQuote | singleQuoteStringContent)* "'" }
}

commaSep<Expr> { commaSep1<Expr>? }
commaSep1<Expr> { Expr ("," Expr?)* }

MapKV { (Identifier | StringLiteral) ":" Expr }
MapSplat { "*" Expr }
MapEntry { MapKV | MapSplat }

@external tokens literalTokens from "./tokens" { Float }

@tokens {
  whitespace { $[ \t\r\n] }

  Pipe { "|" }
  LogicalAndOp { "&&" }
  LogicalOrOp { "||" }
  CompareOp { "<" | ">" | ">=" | "<=" | "==" | "!=" }

  BoolLiteral { "true" | "false" }
  NilLiteral { "nil" }
  Integer { $[0-9]+ }
  HexInteger { "0x" $[0-9a-fA-F_]+ }

  @precedence { HexInteger, Integer }

  LineComment[isolate] { "//" ![\n]* }

  @precedence { LineComment, "/" }

  Identifier { $[a-zA-Z_] $[a-zA-Z0-9_]* }

  If { "if" }
  ElseIf { "else if" }
  Else { "else" }
  Return { "return" }
  Break { "break" }
  @precedence { Return, Break, If, ElseIf, Else, BoolLiteral, NilLiteral, Identifier }

  doubleQuoteStringContent { !["\\\n]+ }
  singleQuoteStringContent { !['\\\n]+ }

  "[" "]" "{" "}" "(" ")"
  ":" ","
  "=" ".." "..="
  "&" "!" "->" ";"
}

@external propSource geoscriptHighlighting from "./highlight"

@detectDelim
